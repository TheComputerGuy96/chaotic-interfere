
_pkgname="yuzu"
pkgname="$_pkgname-git"
pkgver=r25700.57c8dcfd7
pkgrel=1
pkgdesc='An experimental open-source emulator for the Nintendo Switch'
url="https://github.com/yuzu-emu/yuzu"
license=('GPL2')
arch=('i686' 'x86_64')

depends=(
  'boost-libs'
  'cubeb'
  'enet'
  'fmt'
  'libinih'
  'mbedtls'
  'qt5-multimedia'
  'qt5-webengine'
  'sdl2'
)
makedepends=(
  'boost'
  'catch2'
  'cmake'
  'ffmpeg'
  'gcc'
  'git'
  'glslang'
  'llvm'
  'mold'
  'ninja'
  'nlohmann-json'
  'qt5-tools'
  'rapidjson'
  'robin-map'
  'spirv-headers'
  'vulkan-headers'
)
optdepends=(
  'chaotic-interfere: chaotic-aur interference tracker'
  "qt5-wayland: Wayland support"
)

provides=("$_pkgname")
conflicts=("$_pkgname")

_pkgsrc="$_pkgname"
source=("$_pkgsrc"::"git+$url.git")
sha256sums=('SKIP')

_submodules_yuzu() {
  source+=(
    # submodules for yuzu
    'yuzu-emu.discord-rpc'::'git+https://github.com/yuzu-emu/discord-rpc.git'
    'bylaws.libadrenotools'::'git+https://github.com/bylaws/libadrenotools.git'
    'herumi.xbyak'::'git+https://github.com/herumi/xbyak.git'
    'lsalzman.enet'::'git+https://github.com/lsalzman/enet.git'
    'khronosgroup.vulkan-headers'::'git+https://github.com/KhronosGroup/Vulkan-Headers.git'
    'merryhime.dynarmic'::'git+https://github.com/merryhime/dynarmic.git'
    'yuzu-emu.sirit'::'git+https://github.com/yuzu-emu/sirit.git'
    'libusb'::'git+https://github.com/libusb/libusb.git'
    'mozilla.cubeb'::'git+https://github.com/mozilla/cubeb.git'
    'libsdl-org.sdl'::'git+https://github.com/libsdl-org/SDL.git'
    'benhoyt.inih'::'git+https://github.com/benhoyt/inih.git'
    'microsoft.vcpkg'::'git+https://github.com/microsoft/vcpkg.git'
    'gpuopen-librariesandsdks.vulkanmemoryallocator'::'git+https://github.com/GPUOpen-LibrariesAndSDKs/VulkanMemoryAllocator.git'
    'ffmpeg'::'git+https://github.com/FFmpeg/FFmpeg.git'
    'yhirose.cpp-httplib'::'git+https://github.com/yhirose/cpp-httplib.git'
    'lat9nq.tzdb_to_nx'::'git+https://github.com/lat9nq/tzdb_to_nx.git'
    'xiph.opus'::'git+https://github.com/xiph/opus.git'
    'arun11299.cpp-jwt'::'git+https://github.com/arun11299/cpp-jwt.git'
    'yuzu-emu.mbedtls'::'git+https://github.com/yuzu-emu/mbedtls.git'
    'yuzu-emu.breakpad'::'git+https://github.com/yuzu-emu/breakpad.git'
  )
  sha256sums+=(
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
  )

  _prepare_yuzu() (
    # submodules for yuzu
    cd "${srcdir:?}/$_pkgsrc"
    local -A _submodules=(
      ['yuzu-emu.discord-rpc']='externals/discord-rpc'
      ['bylaws.libadrenotools']='externals/libadrenotools'
      ['herumi.xbyak']='externals/xbyak'
      ['lsalzman.enet']='externals/enet'
      ['khronosgroup.vulkan-headers']='externals/Vulkan-Headers'
      ['merryhime.dynarmic']='externals/dynarmic'
      ['yuzu-emu.sirit']='externals/sirit'
      ['libusb']='externals/libusb/libusb'
      ['mozilla.cubeb']='externals/cubeb'
      ['libsdl-org.sdl']='externals/SDL'
      ['benhoyt.inih']='externals/inih/inih'
      ['microsoft.vcpkg']='externals/vcpkg'
      ['gpuopen-librariesandsdks.vulkanmemoryallocator']='externals/VulkanMemoryAllocator'
      ['ffmpeg']='externals/ffmpeg/ffmpeg'
      ['yhirose.cpp-httplib']='externals/cpp-httplib'
      ['lat9nq.tzdb_to_nx']='externals/nx_tzdb/tzdb_to_nx'
      ['xiph.opus']='externals/opus'
      ['arun11299.cpp-jwt']='externals/cpp-jwt'
      ['yuzu-emu.mbedtls']='externals/mbedtls'
      ['yuzu-emu.breakpad']='externals/breakpad'
    )
    for key in ${!_submodules[@]} ; do
      git submodule init "${_submodules[${key}]}"
      git submodule set-url "${_submodules[${key}]}" "${srcdir}/${key}"
      git -c protocol.file.allow=always submodule update "${_submodules[${key}]}"
    done
  )
}

_submodules_bylaws_libadrenotools() {
  source+=(
    # submodules for bylaws.libadrenotools
    'bylaws.liblinkernsbypass'::'git+https://github.com/bylaws/liblinkernsbypass.git'
  )
  sha256sums+=(
    'SKIP'
  )

  _prepare_bylaws_libadrenotools() (
    # submodules for bylaws.libadrenotools
    cd "${srcdir:?}/$_pkgsrc"
    cd "externals/libadrenotools"
    local -A _submodules=(
      ['bylaws.liblinkernsbypass']='lib/linkernsbypass'
    )
    for key in ${!_submodules[@]} ; do
      git submodule init "${_submodules[${key}]}"
      git submodule set-url "${_submodules[${key}]}" "${srcdir}/${key}"
      git -c protocol.file.allow=always submodule update "${_submodules[${key}]}"
    done
  )
}

_submodules_lat9nq_tzdb_to_nx() {
  source+=(
    # submodules for lat9nq.tzdb_to_nx
    'eggert.tz'::'git+https://github.com/eggert/tz.git'
  )
  sha256sums+=(
    'SKIP'
  )

  _prepare_lat9nq_tzdb_to_nx() (
    # submodules for lat9nq.tzdb_to_nx
    cd "${srcdir:?}/$_pkgsrc"
    cd "externals/nx_tzdb/tzdb_to_nx"
    local -A _submodules=(
      ['eggert.tz']='externals/tz/tz'
    )
    for key in ${!_submodules[@]} ; do
      git submodule init "${_submodules[${key}]}"
      git submodule set-url "${_submodules[${key}]}" "${srcdir}/${key}"
      git -c protocol.file.allow=always submodule update "${_submodules[${key}]}"
    done
  )
}

_submodules_mozilla_cubeb() {
  source+=(
    # submodules for mozilla.cubeb
    'google.googletest'::'git+https://github.com/google/googletest.git'
    'arsenm.sanitizers-cmake'::'git+https://github.com/arsenm/sanitizers-cmake.git'
  )
  sha256sums+=(
    'SKIP'
    'SKIP'
  )

  _prepare_mozilla_cubeb() (
    # submodules for mozilla.cubeb
    cd "${srcdir:?}/$_pkgsrc"
    cd "externals/cubeb"
    local -A _submodules=(
      ['google.googletest']='googletest'
      ['arsenm.sanitizers-cmake']='cmake/sanitizers-cmake'
    )
    for key in ${!_submodules[@]} ; do
      git submodule init "${_submodules[${key}]}"
      git submodule set-url "${_submodules[${key}]}" "${srcdir}/${key}"
      git -c protocol.file.allow=always submodule update "${_submodules[${key}]}"
    done
  )
}

_submodules_yuzu_emu_sirit() {
  source+=(
    # submodules for yuzu-emu.sirit
    'khronosgroup.spirv-headers'::'git+https://github.com/KhronosGroup/SPIRV-Headers.git'
  )
  sha256sums+=(
    'SKIP'
  )

  _prepare_yuzu_emu_sirit() (
    # submodules for yuzu-emu.sirit
    cd "${srcdir:?}/$_pkgsrc"
    cd "externals/sirit"
    local -A _submodules=(
      ['khronosgroup.spirv-headers']='externals/SPIRV-Headers'
    )
    for key in ${!_submodules[@]} ; do
      git submodule init "${_submodules[${key}]}"
      git submodule set-url "${_submodules[${key}]}" "${srcdir}/${key}"
      git -c protocol.file.allow=always submodule update "${_submodules[${key}]}"
    done
  )
}

_submodules_yuzu
_submodules_bylaws_libadrenotools
_submodules_lat9nq_tzdb_to_nx
_submodules_mozilla_cubeb
_submodules_yuzu_emu_sirit

pkgver() {
  cd "$_pkgsrc"
  printf 'r%s.%s' \
    "$(git rev-list --count HEAD)" \
    "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$_pkgsrc"

  _prepare_yuzu

  _prepare_bylaws_libadrenotools
  _prepare_lat9nq_tzdb_to_nx
  _prepare_mozilla_cubeb
  _prepare_yuzu_emu_sirit
}

build() {
  local _cmake_options=(
    -S "$_pkgsrc"
    -B build
    -GNinja
    -DCMAKE_INSTALL_PREFIX="/usr"
    -DCMAKE_C_COMPILER="gcc"
    -DCMAKE_CXX_COMPILER="g++"
    -DCMAKE_C_FLAGS="$CFLAGS"
    -DCMAKE_CXX_FLAGS="$CXXFLAGS"
    -DCMAKE_BUILD_TYPE="Release"

    -DYUZU_DOWNLOAD_TIME_ZONE_DATA=ON
    -DYUZU_ENABLE_LTO=ON
    -DYUZU_TESTS=OFF
    -DYUZU_USE_BUNDLED_FFMPEG=OFF
    -DYUZU_USE_BUNDLED_QT=OFF
    -DYUZU_USE_FASTER_LD=OFF
    -DYUZU_USE_EXTERNAL_SDL2=OFF
    -DYUZU_USE_EXTERNAL_VULKAN_HEADERS=OFF
    -DYUZU_USE_QT_MULTIMEDIA=ON
    -DYUZU_USE_QT_WEB_ENGINE=ON
    -DENABLE_QT6=OFF
    -DENABLE_QT_TRANSLATION=ON
    -DUSE_DISCORD_PRESENCE=ON
    -DSIRIT_USE_SYSTEM_SPIRV_HEADERS=ON

    -DBUILD_REPOSITORY=yuzu-emu/yuzu
    -DBUILD_TAG="${pkgver}"
    -DTITLE_BAR_FORMAT_IDLE="yuzu | ${pkgver} {}"
    -DTITLE_BAR_FORMAT_RUNNING="yuzu | ${pkgver} | {}"
    -Wno-dev

    -DDYNARMIC_NO_BUNDLED_ROBIN_MAP=ON
    -DENABLE_COMPATIBILITY_LIST_DOWNLOAD=ON
    -DYUZU_USE_BUNDLED_LIBUSB=OFF
    -DYUZU_USE_BUNDLED_OPUS=OFF
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="${pkgdir:?}" cmake --install build
}
