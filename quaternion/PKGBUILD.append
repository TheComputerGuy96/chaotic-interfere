_pkgname="quaternion"
pkgname="$_pkgname"
pkgver=0.0.95.1
pkgrel=2
pkgdesc='Qt-based IM client for the Matrix protocol'
#url="https://matrix.org/docs/projects/client/quaternion.html"
url="https://github.com/quotient-im/Quaternion"
arch=('i686' 'x86_64')
license=(GPL3)

depends=(
  hicolor-icon-theme
  qt5-multimedia
  qt5-quickcontrols2
  qt5-tools
  qtkeychain-qt5
)
makedepends=(
  cmake
  git
)
optdepends=(
  'qt5-graphicaleffects'
  'qtkeychain: Store access tokens in a keyring'
)

_pkgsrc="$_pkgname"
source=(
  "$_pkgsrc"::"git+https://github.com/quotient-im/Quaternion#tag=$pkgver"
  "libquotient"::'git+https://github.com/quotient-im/libQuotient'
)
sha256sums=(
  'SKIP'
  'SKIP'
)

prepare() {
  ( # submodules - quoternion
    cd "$_pkgsrc"
    git submodule init "lib"
    git submodule set-url "lib" "${srcdir}/libquotient"
    git -c protocol.file.allow=always submodule update "lib"
  )
}

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -DCMAKE_INSTALL_PREFIX="/usr"
    -DCMAKE_BUILD_TYPE=Release
    -DUSE_INTREE_LIBQMC=ON
    -DBUILD_WITH_QT6=OFF
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
