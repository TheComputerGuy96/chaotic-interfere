
_pkgname="glaxnimate"
pkgname="$_pkgname-git"
pkgver=0.5.4.r34.g9c9677a5
pkgrel=1
pkgdesc="Simple vector animation program"
# https://glaxnimate.mattbas.org/
url="https://gitlab.com/mattbas/glaxnimate"
license=('GPL3')
arch=('x86_64' 'i686' 'armv7h' 'aarch64' 'riscv32' 'riscv64')

depends=(
  'ffmpeg'
  'hicolor-icon-theme'
  'libarchive'
  'potrace'
  'python'
  'qt6-base'
  'qt6-imageformats'
  'qt6-svg'
  'qt6-tools'
  'zlib'
)
makedepends=(
  'clang'
  'cmake'
  'gcc'
  'git'
  'make'
  'qt6-declarative'
)
optdepends=(
  'chaotic-interfere: chaotic-aur interference tracker'
)

if [ x"$pkgname" == x"$_pkgname" ] ; then
  # normal package
  _pkgsrc="$_pkgname"
  source+=("$_pkgsrc"::"git+$url.git#tag=${pkgver%%.r*}")
  sha256sums+=('SKIP')

  pkgver() {
    echo "${pkgver%%.r*}"
  }
else
  # git package
  provides=("$_pkgname")
  conflicts=("$_pkgname")

  _pkgsrc="$_pkgname"
  source+=("$_pkgsrc"::"git+$url.git")
  sha256sums+=('SKIP')

  pkgver() {
    cd "$_pkgsrc"
    git describe --long --tags --exclude='*[a-zA-Z][a-zA-Z]*' | sed -E 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
  }
fi

source+=(
  # submodules for glaxnimate
  'Qt-Color-Widgets'::'git+https://gitlab.com/mattbas/Qt-Color-Widgets.git'
  'Qt-History-LineEdit'::'git+https://gitlab.com/mattbas/Qt-History-LineEdit.git'
  'breeze-icons'::'git+https://github.com/KDE/breeze-icons.git'
  'cmake'::'git+https://gitlab.com/mattbas/CMake-Lib.git'
  'cmake-modules'::'git+https://github.com/rpavlik/cmake-modules.git'
  'pybind11'::'git+https://github.com/pybind/pybind11.git'
  'python-lottie'::'git+https://gitlab.com/mattbas/python-lottie.git'
)
sha256sums+=(
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
)

prepare() {
  (
    # submodules for glaxnimate
    cd "$_pkgsrc"
    local -A _submodules=(
      ['cmake']='cmake'
      ['Qt-History-LineEdit']='external/Qt-History-LineEdit'
      ['Qt-Color-Widgets']='external/Qt-Color-Widgets'
      ['breeze-icons']='data/icons/breeze-icons'
      ['python-lottie']='data/lib/python-lottie'
      ['cmake-modules']='external/cmake-modules'
      ['pybind11']='external/QtAppSetup/external/pybind11'
    )
     for key in ${!_submodules[@]} ; do
      git submodule init "${_submodules[${key}]}"
      git submodule set-url "${_submodules[${key}]}" "${srcdir}/${key}"
      git -c protocol.file.allow=always submodule update "${_submodules[${key}]}"
    done
  )
}

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DCMAKE_BUILD_TYPE=Release
  )

  export MAKEFLAGS="-j8"

  cmake "${_cmake_options[@]}"
  cmake --build build
  cmake --build build --target translations
}

package() {
  DESTDIR="${pkgdir:?}" cmake --install build
}
