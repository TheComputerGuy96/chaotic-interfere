
# options
if [ -n "$_srcinfo" ] || [ -n "$_pkgver" ] ; then
  : ${_autoupdate:=false}
else
  : ${_autoupdate:=true}
fi

: ${_pkgtype:=stable}

# basic info
_pkgname="elixir"
pkgname="$_pkgname${_pkgtype:+-$_pkgtype}"
pkgver=1.15.7
pkgrel=1
pkgdesc="a functional meta-programming aware language built on top of the Erlang VM"
#url="https://elixir-lang.org"
url="https://github.com/elixir-lang/elixir"
license=('Apache' 'custom:EPL')
arch=('any')

# main package
_main_package() {
  _update_version

  depends=('erlang-nox')
  checkdepends=('git')
  optdepends=(
    'chaotic-interfere: chaotic-aur interference tracker'
  )

  conflicts=("$_pkgname=${pkgver:?}")
  provides=("$_pkgname")

  _pkgsrc="$_pkgname-${_pkgver:?}"
  source=("$_pkgsrc.tar.gz::https://github.com/elixir-lang/elixir/archive/v$_pkgver.tar.gz")
  sha256sums=('SKIP')
}

# common functions
pkgver() {
  printf '%s' "${_pkgver//-rc./rc}"
}

build() {
  cd "$_pkgsrc"
  make
}

package() {
  cd "$_pkgsrc"
  install -Dm644 "LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
  make DESTDIR="$pkgdir" PREFIX=/usr install
}

# update version
_update_version() {
  : ${_pkgver:=$pkgver}

  if [[ "${_autoupdate::1}" != "t" ]] ; then
    return
  fi

  local _response=$(curl -Ssf "$url/releases.atom")
  local _tag=$(
    printf '%s' "$_response" \
      | grep '/releases/tag/' \
      | sed -E 's@^.*/releases/tag/(.*)".*$@\1@' \
      | grep -Ev '[a-z]{2}' | sort -rV | head -1
  )
  local _pkgver_new="${_tag#v}"

  # update _pkgver
  if [[ "${_pkgver_new:?}" =~ ^[0-9.]+$ ]]; then
    if [ "$_pkgver" != "${_pkgver_new:?}" ] ; then
      _pkgver="${_pkgver_new:?}"
    fi
  else
    echo "Error: release candidate or other unusual version detected: '$_pkgver_new'"
    unset _pkgver
  fi
}

# execute
_main_package
