
_pkgname=flameshot
pkgname="$_pkgname-git"
pkgver=12.1.0.r119.gfa29bcb
pkgrel=1
pkgdesc="Powerful yet simple to use screenshot software"
url="https://github.com/flameshot-org/flameshot"
license=('GPL')
arch=('x86_64')

depends=(
  'qt5-svg'
  'hicolor-icon-theme'
  'kguiaddons5'
)
makedepends=(
  'make'
  'qt5-tools'
  'cmake'
)
optdepends=(
  'chaotic-interfere: chaotic-aur interference tracker'
  'gnome-shell-extension-appindicator: for system tray icon if you are using Gnome'
  'grim: for wlroots wayland support'
  'xdg-desktop-portal: for wayland support, you will need the implementation for your wayland desktop environment'
)


if [ x"$pkgname" == x"$_pkgname" ] ; then
  # normal package
  _pkgsrc="$_pkgname-${pkgver%%.r*}"
  _pkgext="tar.gz"

  source+=("$_pkgsrc.$_pkgext"::"$url/archive/v${pkgver%%.r*}.$_pkgext")
  sha256sums+=(
    'c82c05d554e7a6d810aca8417ca12b21e4f74864455ab4ac94602668f85ac22a'
  )

  pkgver() {
    echo "${pkgver%%.r*}"
  }
else
  # git package
  makedepends+=('git')

  provides=("$_pkgname=${pkgver%%.r*}")
  conflicts=("$_pkgname")

  _pkgsrc="$_pkgname"
  source+=("$_pkgsrc"::"git+$url.git")
  sha256sums+=('SKIP')

  pkgver() {
    cd "$_pkgsrc"
    _tag=$(git tag | grep -Ev '[a-z]{2}' | sort -V | tail -1)
    _revision=$(git rev-list --count $_tag..HEAD)
    _commit=$(git rev-parse --short=7 HEAD)

    printf '%s.r%s.g%s' "${_tag#v}" "$_revision" "$_commit"
  }
fi

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX=/usr
    -DUSE_WAYLAND_CLIPBOARD=1
    -DUSE_WAYLAND_GRIM=true
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="${pkgdir:?}" cmake --install build

  # zsh _flameshot completion is provided by zsh-completions so exclude from packaging
  rm -rf ${pkgdir}/usr/share/zsh/
}
