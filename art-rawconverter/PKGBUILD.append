
: ${_clang:=true}

_pkgname="art-rawconverter"
: ${pkgname:="$_pkgname-git"}
: ${pkgver:=1.20.2.r9.gc2a681faf}
pkgrel=3
pkgdesc="Raw image Converter forked from RawTherapee with ease of use in mind"
url="https://bitbucket.org/agriggio/art"
license=('GPL3')
arch=('x86_64')

depends=(
  'desktop-file-utils'
  'exiv2'
  'fftw'
  'glibmm'
  'gtk3'
  'gtkmm3'
  'lcms2'
  'lensfun'
  'libcanberra'
  'libiptcdata'
  'libraw'
  'mimalloc'
  'opencolorio'
  'openmp'
)
makedepends=(
  'cmake'
  'fakeroot'
  'gcc'
  'git'
  'hicolor-icon-theme'
  'pkgconf'
)
optdepends=(
  'chaotic-interfere: chaotic-aur interference tracker'
  'art-rawconverter-imageio: add support for additional image formats'
  'lcms2-ff: lcms2 with fast-float plugin for improved export speed'
  'perl-image-exiftool: metadata support for CR3 images'
)

[[ x"{$_clang::1}" == "xt" ]] && makedepends+=('clang' 'lld' 'llvm')

if [ x"$pkgname" == x"$_pkgname" ] ; then
  # normal package
  _pkgsrc="$_pkgname"
  source+=("$_pkgsrc"::"git+$url.git#tag=${pkgver%%.r*}")
  sha256sums+=('SKIP')

  pkgver() {
    echo "${pkgver%%.r*}"
  }
else
  # git package
  provides=("$_pkgname")
  conflicts=("$_pkgname")

  _pkgsrc="$_pkgname"
  source+=("$_pkgsrc"::"git+$url.git")
  sha256sums+=('SKIP')

  pkgver() {
    cd "$_pkgsrc"
    git describe --long --tags --exclude='*[a-zA-Z][a-zA-Z]*' | sed -E 's/^v//;s/([^-]*-g)/r\1/;s/-/./g'
  }
fi

prepare() {
  # fix version in about dialog
  sed -E \
    -e 's@--exact-match@--exclude="*[a-zA-Z][a-zA-Z]*" | sed -E "s/^v//;s/([^-]*-g)/r\1/;s/-/./g;s/\$/ (aur.chaotic.cx)/" @' \
    -i "${srcdir:?}/$_pkgsrc/UpdateInfo.cmake"
}

build() {
  if [[ x"$pkgname" != _"$_pkgname" ]] ; then
    # apply CPU optimizations only for git package
    export CFLAGS="$(echo "$CFLAGS" | sed -E 's@(\s*-(march|mtune)=\S+\s+)@ @g;s@$@ -march=broadwell -mtune=skylake@')"
    export CXXFLAGS="$(echo "$CFLAGS" | sed -E 's@(\s*-(march|mtune)=\S+\s+)@ @g;s@$@ -march=broadwell -mtune=skylake@')"
  fi

  local _cmake_options=(
    -B build
    -S "$_pkgsrc"
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_LTO=ON
    -DENABLE_LIBRAW=ON
    -DENABLE_OCIO=ON
    -DBUILD_SHARED=ON
    -Wno-dev
  )

  [[ x"{$_clang::1}" == "xt" ]] && _cmake_options+=(
    -DCMAKE_C_COMPILER=clang
    -DCMAKE_CXX_COMPILER=clang++
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="${pkgdir:?}" cmake --install build

  # symlinks
  ln -s "ART" "${pkgdir:?}/usr/bin/art"
  ln -s "ART-cli" "${pkgdir:?}/usr/bin/art-cli"
}
