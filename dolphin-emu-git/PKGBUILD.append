_pkgname="dolphin-emu"
pkgname="$_pkgname-git"
pkgver=5.0.r20237.gc87b2e46b4
pkgrel=1
pkgdesc='A Gamecube / Wii / Triforce emulator'
arch=(x86_64)
url="https://dolphin-emu.org"
license=(GPL2)
depends=(
  # extra/dolphin-emu
  alsa-lib
  bluez-libs
  enet
  gcc-libs
  glibc
  hidapi
  libavcodec.so
  libavformat.so
  libavutil.so
  libcurl.so
  libevdev
  libgl
  libminiupnpc.so
  libpulse
  libsfml-network.so
  libsfml-system.so
  libswscale.so
  libudev.so
  libusb-1.0.so
  libx11
  libxi
  libxrandr
  lzo
  minizip-ng
  pugixml
  qt6-base
  qt6-svg
  sfml

  ## dolphin-emu-git
  #libmgba
  cubeb
  fmt
  libspng
  mbedtls2
  zlib-ng
)
makedepends+=(
  cmake
  git
  ninja
  python
)
optdepends=(
  'pulseaudio: PulseAudio backend'
)

options=(!emptydirs !lto)

if [ x"$pkgname" == x"$_pkgname" ] ; then
  # normal package
  :
else
  # git package
  url="https://github.com/dolphin-emu/dolphin"

  provides=("$_pkgname")
  conflicts=("$_pkgname")

  _pkgsrc="$_pkgname"
  source[0]=
  source=(
    "$_pkgname"::"git+$url.git"

    ## submodules - dolphin-emu
    #'FFmpeg-bin'::'git+https://github.com/dolphin-emu/ext-win-ffmpeg.git'
    #'Qt'::'git+https://github.com/dolphin-emu/ext-win-qt.git'
    #'SDL'::'git+https://github.com/libsdl-org/SDL.git'
    #'SPIRV-Cross'::'git+https://github.com/KhronosGroup/SPIRV-Cross.git'
    #'cubeb'::'git+https://github.com/mozilla/cubeb.git'
    #'curl'::'git+https://github.com/curl/curl.git'
    #'fmt'::'git+https://github.com/fmtlib/fmt.git'
    #'gtest'::'git+https://github.com/google/googletest.git'
    #'libadrenotools'::'git+https://github.com/bylaws/libadrenotools.git'
    #'libspng'::'git+https://github.com/randy408/libspng.git'
    #'libusb'::'git+https://github.com/libusb/libusb.git'
    #'lz4'::'git+https://github.com/lz4/lz4'
    #'zlib-ng'::'git+https://github.com/zlib-ng/zlib-ng.git'
    'VulkanMemoryAllocator'::'git+https://github.com/GPUOpen-LibrariesAndSDKs/VulkanMemoryAllocator.git'
    'implot'::'git+https://github.com/epezent/implot.git'
    'mgba'::'git+https://github.com/mgba-emu/mgba.git'
    'rcheevos'::'git+https://github.com/RetroAchievements/rcheevos.git'

  )
  sha256sums+=(
    'SKIP'

    #'SKIP'
    #'SKIP'
    #'SKIP'
    #'SKIP'
    #'SKIP'
    #'SKIP'
    #'SKIP'
    #'SKIP'
    #'SKIP'
    #'SKIP'
    #'SKIP'
    #'SKIP'
    #'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
    'SKIP'
  )

  pkgver() {
    cd "$_pkgsrc"
    git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
  }
fi

prepare() {
  # Fix minizip-ng name for Arch
  sed -E -e 's@(pkgconfig\(MINIZIP minizip)([^a-z]+)@\1-ng\2@' \
    -i "${srcdir:?}/$_pkgsrc/CMakeLists.txt"

  (
    # submodules - dolphin-emu
    cd "$_pkgname"
    local _submodules=(
      #'Externals/FFmpeg-bin'
      #'Externals/Qt'
      #'Externals/SDL/SDL'
      #'Externals/cubeb/cubeb'
      #'Externals/curl/curl'
      #'Externals/fmt/fmt'
      #'Externals/gtest'
      #'Externals/libadrenotools'
      #'Externals/libspng/libspng'
      #'Externals/libusb/libusb'
      #'Externals/lz4/lz4'
      #'Externals/spirv_cross/SPIRV-Cross'
      #'Externals/zlib-ng/zlib-ng'
      'Externals/VulkanMemoryAllocator'
      'Externals/implot/implot'
      'Externals/mGBA/mgba'
      'Externals/rcheevos/rcheevos'
    )
    for submodule in "${_submodules[@]}" ; do
      git submodule init "${submodule}"
      git submodule set-url "${submodule}" "${srcdir}/${submodule##*/}"
      git -c protocol.file.allow=always submodule update "${submodule}"
    done
  )
}

build() {
  local _cmake_options=(
    -B build
    -S "$_pkgname"
    -G Ninja
    -DCMAKE_BUILD_TYPE=None
    -DCMAKE_INSTALL_PREFIX='/usr'
    -DDISTRIBUTOR='aur.chaotic.cx'
    -DENABLE_TESTS=OFF
    -DENABLE_AUTOUPDATE=OFF
    -DUSE_SYSTEM_LIBS=ON
    -DUSE_SYSTEM_LIBMGBA=OFF
    -Wno-dev
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}

package() {
  DESTDIR="${pkgdir:?}" cmake --install build

  install -Dm644 "${srcdir:?}/$_pkgsrc/Data/51-usb-device.rules" \
    -t "${pkgdir:?}/usr/lib/udev/rules.d/"

  rm -rf "${pkgdir:?}"/usr/{include,lib/libdiscord-rpc.a}
}
