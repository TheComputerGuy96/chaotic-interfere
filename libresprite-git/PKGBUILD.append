# Maintainer:
# Contributor: GodofGrunts <me@godofgrunts.xyz>

_pkgname="libresprite"
pkgname="$_pkgname-git"
pkgver=1.0.r35.gbd5a0fc82
pkgrel=1
pkgdesc='Animated sprite editor and pixel art tool'
arch=('x86_64' 'i686')
url='https://github.com/LibreSprite/LibreSprite'
license=('GPL')
depends=(
  'curl'
  'freetype2'
  'giflib'
  'libjpeg-turbo'
  'libpng'
  'libwebp'
  'lua'
  'pixman'
  'sdl2'
  'sdl2_image'
  'tinyxml'
  'zlib'
)
makedepends=(
  #'ninja'
  'cmake'
  'git'
  'gtest'
)

source=(
  "$_pkgname"::"git+https://github.com/LibreSprite/LibreSprite.git"
  "clip"::"git+https://github.com/aseprite/clip.git"
  "duktape"::"git+https://github.com/LibreSprite/duktape.git"
  "flic"::"git+https://github.com/aseprite/flic.git"
  "observable"::"git+https://github.com/dacap/observable.git"
  "simpleini"::"git+https://github.com/aseprite/simpleini.git"
  "undo"::"git+https://github.com/aseprite/undo.git"
  "libresprite.desktop"
)

sha256sums=(
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'SKIP'
  'd246c14dc6c0d71d02aba337c15c23611c6e982687300c86167f37b9926af7ba'
)

provides=(
  libresprite
)
conflicts=(
  aseprite
  libresprite
)

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$_pkgname"

  _submodules=(
    'src/clip'
    'src/flic'
    'src/observable'
    'src/undo'
    'third_party/duktape'
    'third_party/simpleini'
  )
  for submodule in ${_submodules[@]} ; do
    git submodule init ${submodule}
    git submodule set-url ${submodule} "${srcdir}/${submodule##*/}"
    git -c protocol.file.allow=always submodule update ${submodule}
  done
}

build() {
  _cmake_options=(
    -B build
    -S "$_pkgname"

    -DCMAKE_INSTALL_PREFIX="/usr"
  )

  cmake "${_cmake_options[@]}"
  cmake --build build
}


package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm644 "$srcdir/libresprite.desktop" \
    -t "$pkgdir/usr/share/applications"

  install -Dm644 "$pkgdir/usr/share/libresprite/data/icons/ase64.png" \
    "$pkgdir/usr/share/pixmaps/libresprite.png"
}

