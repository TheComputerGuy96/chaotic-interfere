
# options
if [ -n "$_srcinfo" ] || [ -n "$_pkgver" ] ; then
  : ${_autoupdate:=false}
else
  : ${_autoupdate:=true}
fi

# basic info
_snapname="authy"
_pkgname="authy"
pkgname="$_pkgname${_pkgtype:-}"
pkgver=2.4.2
pkgrel=1
pkgdesc="Two factor authentication desktop application"
url="https://snapcraft.io/$_snapname"
# url='https://authy.com/'
license=('custom')
arch=('x86_64')

# main package
_main_package() {
  _update_version

  makedepends=(
    'html-xml-utils'
    'pandoc'
    'sha3sum'
    'squashfs-tools'
  )
  optdepends=(
    'chaotic-interfere: chaotic-aur interference tracker'
  )

  source=("$_filename"::"$_dl_url")
  sha256sums=('SKIP')
}

# common functions
_privacy_policy="twilio_privacy_policy"
_privacy_policy_url="https://www.twilio.com/en-us/legal/privacy"
_terms_of_use="authy_terms_of_use"
_terms_of_use_url="https://www.twilio.com/en-us/legal/authy-app-terms"
_acceptable_use_policy="acceptable_use_policy"
_acceptable_use_policy_url="https://www.twilio.com/en-us/legal/aup"

prepare() {
  # check upstream sha3-384sum
  printf '%s  %s' "$_cksum" "$_filename" | sha3-384sum -c

  # privacy policy
  curl --no-progress-meter \
    -o "$_privacy_policy-1.html" \
    "$_privacy_policy_url"

  hxnormalize -x "$_privacy_policy-1.html" \
    | hxselect .grid-container-column \
    1> "$_privacy_policy-2.html" \
    2> /dev/null

  pandoc --quiet -t plain -o "$_privacy_policy.txt" "$_privacy_policy-2.html"

  # terms of use 
  curl --no-progress-meter \
    -o "$_terms_of_use-1.html" \
    "$_terms_of_use_url"

  hxnormalize -x "$_terms_of_use-1.html" \
    | hxselect 'h1,p' \
    | hxremove '.navigation-dropdown-link-description' \
    | sed -E \
      -e 's@<p><a @<p class="discard"><a @g' \
      -e 's@<p>(Data is collected based on search terms)@<p class="discard">\1@g' \
    | hxremove .discard \
    | hxnormalize \
    1> "$_terms_of_use-2.html" \
    2> /dev/null

  pandoc --quiet -f html -t plain -o "$_terms_of_use.txt" "$_terms_of_use-2.html"

  # acceptable_use_policy 
  curl --no-progress-meter \
    -o "$_terms_of_use-1.html" \
    "$_terms_of_use_url"

  hxnormalize -x "$_terms_of_use-1.html" \
    | hxselect 'h1,p' \
    | hxremove '.navigation-dropdown-link-description' \
    | sed -E \
      -e 's@<p><a @<p class="discard"><a @g' \
      -e 's@<p>(Data is collected based on search terms)@<p class="discard">\1@g' \
    | hxremove .discard \
    | hxnormalize \
    1> "$_terms_of_use-2.html" \
    2> /dev/null

  pandoc --quiet -f html -t plain -o "$_terms_of_use.txt" "$_terms_of_use-2.html"

  ! true

  # unpack
  mkdir -p "$_pkgname-$_pkgver"
  unsquashfs -q -n -f -d "$_pkgname-$_pkgver/" "$_filename"

  mv "$_pkgname-$_pkgver/meta/gui/$_snapname.desktop" "$srcdir/$_pkgname.desktop"
  sed -i -e 's@${SNAP}/meta/gui/icon.png@'"${_pkgname}"'@g' \
    "$srcdir/$_pkgname.desktop"

  mv "$_pkgname-$_pkgver/meta/gui/icon.png" "$srcdir"
  rm -r "$_pkgname-$_pkgver/"{data-dir/,gnome-platform/,lib/,meta/,scripts/,usr/,*.sh}
}

package() {
  depends+=(
    'nss'
    'gtk3'
  )

  # opt
  install -d "$pkgdir/opt/$_pkgname"
  cp -r "$srcdir/$_pkgname-$pkgver/"* "$pkgdir/opt/$_pkgname"

  # symlink binary
  install -d "$pkgdir/usr/bin"
  ln -s "/opt/$_pkgname/$_pkgname" "$pkgdir/usr/bin/$_pkgname"

  # icons
  install -Dm644 "$srcdir/icon.png" "$pkgdir/usr/share/pixmaps/$_pkgname.png"
  install -Dm644 "$srcdir/$_pkgname.desktop" "$pkgdir/usr/share/applications/$_pkgname.desktop"

  # license/eula
  install -Dm644 "$_privacy_policy.txt" -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 "$_terms_of_use.txt" -t "$pkgdir/usr/share/licenses/$pkgname"
}

# update version
_update_version() {
  : ${_pkgver:=${pkgver%%.r*}}

  if [[ "${_autoupdate::1}" != "t" ]] ; then
    return
  fi

  _response=$(
    curl -H 'Snap-Device-Series: 16' --no-progress-meter "http://api.snapcraft.io/v2/snaps/info/$_snapname" \
      | sed 's@,@,\n@g'
  )

  _dl_url=$(
    printf '%s' "$_response" \
      | grep '"url"' \
      | sed -E 's@^"url":"(https:.*\.snap)".*$@\1@'
  )

  _pkgver_new=$(
    printf '%s' "$_response" \
      | grep '"version"' \
      | sed -E 's@^"version":"([0-9]+\.[0-9]+\.[0-9]+)".*$@\1@'
  )

  # sha3-384 checksum
  _cksum=$(
    printf '%s' "$_response" \
      | grep '"sha3-384"' \
      | sed -E 's@^"sha3-384":"([a-f0-9]+)".*$@\1@'
  )

  _filename="$_pkgname-${_pkgver_new:?}.snap"

  # update _pkgver
  if [ "$_pkgver" != "${_pkgver_new:?}" ] ; then
    _pkgver="${_pkgver_new:?}"
  fi

  pkgver() {
    printf '%s' "${_pkgver:?}"
  }
}

# execute
_main_package
